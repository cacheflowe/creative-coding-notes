<html>
<head>
  <meta charset="utf-8">
  <title>Grass</title>
  <meta name="viewport" content="width=device-width, initial-scale=1, minimum-scale=1, maximum-scale=1, user-scalable=no">
  <link rel="stylesheet" href="lib/reset.css">
</head>
<body>
  <script src="./lib/processing-1.4.1.js"></script> 
  <script type="text/processing" data-processing-target="canvas">

    float bgGreen = .2;
    int numBlades = 10;
    Grass[] blades;

    void setup () {
      size(window.innerWidth, window.innerHeight);
      frameRate(60);
      colorMode(RGB, 1.0, 1.0, 1.0);
      smooth();
      strokeWeight(0.5);
      background(0,bgGreen,0);
      
      blades = new Grass[numBlades];
      for(int i = 0; i < numBlades; i++) {
        blades[i] = new Grass();   
      }
    }

    void draw() {
      for(int i = 0; i < numBlades; i++) {
        blades[i].update();   
      }
    }

    class Grass {
      float x;
      float y;
      float curLength;
      float lengthVelocity;
      float waveIncrementer;
      float waveIncrementerAmount;
      float curSize;
      float sizeVelocity;
      float curRed;
      float redVelocity;
      float curGreen;
      float greenVelocity;
      float curBlue;
      float blueVelocity;
      float bgGreen = .2;

      Grass() {
        reset();
      } 
      
      void reset(){
        // place grass at bottom
        x = random(0,width);
        y = height + 10;
        curLength = 0;
        lengthVelocity = random(1,3);
        curSize = 20;
        sizeVelocity = random(.1, .2);
        waveIncrementer = random( 0, 2*PI );
        waveIncrementerAmount = random( .002, .05 );
        // 
        curGreen = random(.5, 1);
        greenVelocity = random( .0001, .005 );
        curRed = random(0, .25);
        redVelocity = .5 * greenVelocity;
        curBlue = curRed;
        blueVelocity = redVelocity;
      }

      void update() {
        // use some perlin noise with the sin for some natural waviness
        float curX = x + noise(waveIncrementer*.1) * 20 + sin(waveIncrementer) * 10;
        y -= lengthVelocity;
        
        //draw the shape
        stroke(0,bgGreen,0,70);
        fill(curRed,curGreen,curBlue);
        ellipse(curX,y,curSize,curSize);
        
        // increment properties
        curLength -= lengthVelocity;
        curSize -= sizeVelocity;
        if( curGreen > bgGreen ) curGreen -= greenVelocity;
        curRed -= redVelocity;
        curBlue -= blueVelocity;
        waveIncrementer += waveIncrementerAmount;
        
        // start next blade
        if( curSize <= 0 ) {
          reset();
        }
      }
      
    }


  </script>
  <canvas id="canvas"></canvas>
</body>
</html>
